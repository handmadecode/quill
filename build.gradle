plugins
{
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'org.myire'
version = '3.3'


repositories
{
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

// Load external dependencies
apply from: 'dependencies.gradle'

// Load plugin descriptors
apply from: 'pluginspecs.gradle'

// Load the configuration of the pom file to generate
apply from: 'pomspec.gradle'

// Set the location of the generated pom file
generatePomFileForQuillPublication.destination =
        "$buildDir/poms/${project.archivesBaseName}-${project.version}.pom"


ext.sourceAndTargetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(GroovyCompile).configureEach
{
    sourceCompatibility = sourceAndTargetCompatibility
    targetCompatibility = sourceAndTargetCompatibility
}

tasks.withType(JavaCompile).configureEach
{
    sourceCompatibility = sourceAndTargetCompatibility
    targetCompatibility = sourceAndTargetCompatibility
}

tasks.register('sourcesJar', Jar)
{
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('groovydocJar', Jar)
{
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

tasks.register('javadocJar', Jar)
{
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

groovydocJar.dependsOn 'groovydoc'
javadocJar.dependsOn 'javadoc'

build.finalizedBy 'sourcesJar', 'groovydocJar', 'javadocJar', 'generatePomFileForQuillPublication'
